plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.0"

        defaultConfig {
            ndk {
                // no config needed
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }

    kotlinOptions { jvmTarget = '1.8' }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version '3.18.1'
        }
    }
    ndkVersion '23.2.8568313'

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            //withJavadocJar()
        }
    }
}

version '1.0.0'

dependencies {
    api 'org.dashj:dashj-bls:1.0.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn(publishToMavenLocal)
assemble.dependsOn(sourcesJar)
task comps {
    afterEvaluate {
        println("Components: " + components*.name)
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId 'dashj-bls-android'
                version "$version"
                from components.release

                pom {
                    name = 'DashJ-BLS-Android'
                    description = 'Dash BLS Android Library'
                    url = 'httsp://dashj.org'

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'hashengineering'
                            name = 'HashEngineering'
                            email = 'hashengineeringsolutions@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/dashpay/dashj-android.git'
                        developerConnection = 'scm:git:ssh://github.com/dashpay/dashj-android.git'
                        url = 'http://github.com/dashpay/dashj-android.git'
                    }
                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = project.hasProperty('ossrhUsername') ? project.findProperty('ossrhUsername') : ''
                    password = project.hasProperty('ossrhPassword') ? project.findProperty('ossrhPassword') : ''
                }
            }
        }
    }
    signing {
        required { gradle.taskGraph.hasTask("publish") }
        println("Components: " + publishing.publications)
        sign publishing.publications.release
    }
}