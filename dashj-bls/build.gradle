plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jreleaser' version '1.17.0'
}
version "1.0.1-SNAPSHOT"
android {
    namespace "org.dashj.bls"
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 1
        versionName "$version"

        defaultConfig {
            ndk {
                // no config needed
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }

    kotlinOptions { jvmTarget = '17' }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version '3.22.1'
        }
    }
    ndkVersion '29.0.14206865'

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            //withSourcesJar()
            //withJavadocJar()
        }
    }
}

dependencies {
    api 'org.dashj:dashj-bls:1.0.1'
    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

task javadoc(type: Javadoc) {
    source = fileTree(dir: 'src/main/java', include: '**/*.java')
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    failOnError false
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    // Create empty javadoc jar for now
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn(publishToMavenLocal)
assemble.dependsOn(sourcesJar)
task comps {
    afterEvaluate {
        println("Components: " + components*.name)
    }
}

afterEvaluate {
    def projectVersion = version
    
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId 'dashj-bls-android'
                version projectVersion
                from components.release

                pom {
                    name = 'DashJ-BLS-Android'
                    description = 'Dash BLS Android Library'
                    url = 'https://github.com/dashpay/dashj-android'
                    
                    // Override packaging to jar for Maven Central compatibility
                    packaging = 'jar'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'hashengineering'
                            name = 'HashEngineering'
                            email = 'hashengineeringsolutions@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/dashpay/dashj-android.git'
                        developerConnection = 'scm:git:ssh://github.com/dashpay/dashj-android.git'
                        url = 'http://github.com/dashpay/dashj-android.git'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
    jreleaser {
        project {
            name = 'dashj-bls-android'
            description = 'Dash BLS Android Library'
            website = 'https://github.com/dashpay/dashj-android'
            authors = ['HashEngineering']
            license = 'The Apache License, Version 2.0'
            gitRootSearch = true
            version = projectVersion
        }

        signing {
            active = 'ALWAYS'
            armored = true
        }

        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository("$buildDir/staging-deploy")
                    }
                }
                nexus2 {
                    app {
                        active = 'SNAPSHOT'
                        url = 'https://central.sonatype.com/repository/maven-snapshots/'
                        snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                        applyMavenCentralRules = false // must be false for aar
                        snapshotSupported = true
                        closeRepository = true
                        releaseRepository = true
                        stagingRepository("$buildDir/staging-deploy")
                    }
                }
            }
        }
    }
    signing {
        required { gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.release
    }
}