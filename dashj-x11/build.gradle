plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.17.0'
}

version "1.0.0-SNAPSHOT"

android {
    namespace "org.dashj.x11"
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "$version"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    ndkVersion '29.0.14206865'

    publishing {
        singleVariant("release") {
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn(publishToMavenLocal)
assemble.dependsOn(sourcesJar)
task comps {
    afterEvaluate {
        println("Components: " + components*.name)
    }
}

afterEvaluate {
    def projectVersion = version
    
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId = 'dashj-x11-android'
                version projectVersion
                from components.release

                pom {
                    name = 'DashJ X11'
                    description = 'DashJ X11 Library for Android'
                    url = 'https://github.com/dashpay/dashj-android'
                    
                    // Override packaging to jar for Maven Central compatibility
                    packaging = 'jar'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'hashengineering'
                            name = 'HashEngineering'
                            email = 'hashengineeringsolutions@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/dashpay/dashj-android.git'
                        developerConnection = 'scm:git:ssh://github.com/dashpay/dashj-android.git'
                        url = 'http://github.com/dashpay/dashj-android.git'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
    jreleaser {
        project {
            name = 'dashj-x11-android'
            description = 'DashJ X11 Library for Android'
            website = 'https://github.com/dashpay/dashj-android'
            authors = ['HashEngineering']
            license = 'The Apache License, Version 2.0'
            gitRootSearch = true
            version = projectVersion
        }

        signing {
            active = 'ALWAYS'
            armored = true
        }

        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository("$buildDir/staging-deploy")
                    }
                }
                nexus2 {
                    app {
                        active = 'SNAPSHOT'
                        url = 'https://central.sonatype.com/repository/maven-snapshots/'
                        snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                        applyMavenCentralRules = false // must be false for aar
                        snapshotSupported = true
                        closeRepository = true
                        releaseRepository = true
                        stagingRepository("$buildDir/staging-deploy")
                    }
                }
            }
        }
    }
    signing {
        required { gradle.taskGraph.hasTask("publish") }
        sign publishing.publications.release
    }
}


/*
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'DashJ X11'
                packaging 'aar'
                groupId 'org.dashj.android'
                artifactId 'dashj-x11-android'
                version '0.17.5'
                description 'DashJ X11 Libary for Android'
                url 'https://github.com/HashEngineering/dashj-android'

                scm {
                    connection 'scm:svn:https://github.com/HashEngineering/dashj-android'
                    developerConnection 'scm:svn:https://github.com/HashEngineering/dashj-android'
                    url 'https://github.com/HashEngineering/dashj-android'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'hashengineering'
                        name 'HashEngineering'
                        email 'hashengineeringsolutions@gmail.com'
                    }
                }
            }
        }
    }
}*/